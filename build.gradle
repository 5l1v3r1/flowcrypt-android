/*
 * Â© 2016-2019 FlowCrypt Limited. Limitations apply. Contact human@flowcrypt.com
 * Contributors: DenBond7
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    plugins.apply('checkstyle')

    checkstyle {
        configFile rootProject.file('checkstyle.xml')
        toolVersion '8.14'
        showViolations true
        ignoreFailures false
    }

    task('checkstyle', type: Checkstyle) {
        configProperties.checkStyleConfigDir = rootProject.rootDir
        source 'src/androidTest/java'
        source 'src/debug/java'
        source 'src/main/java'
        source 'src/test/java'
        include '**/*.java'
        classpath = files()
    }

    afterEvaluate {
        Task task = tasks.findByName('check')
        if (task != null) {
            task.dependsOn('checkstyle')
        }
    }
}

ext {
    /*Global*/
    appVersionCode = 74
    appVersionName = '0.7.4'
    buildToolsVersion = '28.0.3'
    compileSdkVersion = 28
    targetSdkVersion = 28
    minSdkVersion = 23
    javaSourceCompatibility = '1.7'
    /*https://developer.android.com/jetpack/androidx/androidx-rn*/
    androidxBaseVersion = '1.0.0'
    /*https://developer.android.com/studio/releases/gradle-plugin.html*/
    androidGradleVersion = '3.3.0'
    /*https://developer.android.com/topic/libraries/testing-support-library/release-notes.html
    * https://mvnrepository.com/artifact/androidx.test.espresso*/
    espressoVersion = '3.1.1'
    /*https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit*/
    retrofitVersion = '2.5.0'
    /*https://mvnrepository.com/artifact/com.squareup.okhttp3/logging-interceptor*/
    okhttpVersion = '3.12.1'
    /*https://mvnrepository.com/artifact/com.squareup.leakcanary/leakcanary-android*/
    leakcanaryVersion = '1.6.3'
    /*https://mvnrepository.com/artifact/com.sun.mail/android-mail*/
    javaMailVersion = '1.6.3'
    /*https://mvnrepository.com/artifact/com.github.bumptech.glide/glide*/
    glideVersion = '4.8.0'
    junitVersion = '4.12'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
